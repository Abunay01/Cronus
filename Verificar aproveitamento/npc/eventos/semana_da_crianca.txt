//===== Cronus Script ======================================
//= Evento Semana da Criança
//===== Por: ==================================================
//= Samuray22
//===== Versão Atual: =====================================
//= 1.2
//===== Descrição: =========================================
//= [Aegis Conversion]
//= Indian Ro: Children Week Event
//= Traduzido para PT-BR (Português - Brasil)
//===== Comentários Adicionais: =================================
//= 1.1 First Beta. [Samuray22]
//= 1.2 Tradução. [Linne Ribeiro]
//============================================================

prontera,146,91,5	script	Pandit chacha#child07	61,{
	if(BaseLevel > 50) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "Hoje é um dia especial, que acontece apenas uma vez ao Ano.";
		mes "Se você achar Buquês de Casamento e Rosas Eternas, me procure ~";
		mes "Que eu vou te dar um presente especial.";
		close;
	} else if(BaseLevel < 20) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "Você é um pequeno aventureiro ainda.";
		mes "Quando você crescer mais, volte. hahaha.";
		close;
	} else if(oversea_event9 < 1) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "Bem vindo, pequeno aventureiro!";
		mes "Hoje é um dia especial não?";
		mes "Eu gostaria de dar um presente para pequenos aventureiros como Você...";
		mes "Mais e você? Quer levar o Presente?";
		next;
		if(select("Não. Eu vou levá-lo mais tarde.","Claro, eu quero.") == 1) {
			mes "[Pandit chacha]";
			mes "Isso é muito ruim .... hum ...";
			mes "É só Dropar de alguns Mobs desse Mundo.";
			mes "Que eu te mostro uma maneira de transformalo em Poções de Criança...";
			mes "Mais volte Quando quiser...";
			close;
		}
		set oversea_event9,1;
		getitem 11705,10; //Children's Potion
		mes "[Pandit chacha]";
		mes "Olhe. Esta é uma Poção de Criança.";
		mes "O peso é de apenas 1, mas recuperar muito HP.";
		mes "Se você quiser obter mais, traga-me um Buquê de Casamento e uma Rosa Eterna.";
		close;
	} else if(oversea_event9 == 1) {
		if(countitem(745) > 0 && countitem(748) > 0) {
			mes "[Pandit chacha]";
			mes "Ahha!!";
			mes "Você me fez lembrar da minha bela composição.";
			mes "E você fez um bom trabalho.";
			mes "Mas você poderia me dar um Buquê de Casamento e uma Rosa Eterna? ";
			next;
			if(select("Ainda Não.","Claro, levá-las.") == 1) {
				mes "[Pandit chacha]";
				mes "você não está preparado ainda, fale comigo quando você estiver pronto.";
				close;
			}
			mes "[Pandit chacha]";
			mes "Hahaha~";
			mes "Oh~ Você conseguiu.";
			mes "Aqui, vou trocar por 50 Poções de Criança.";
			mes "Depois de conseguir essas 50 Poções eu não poderei te dar mais Nada.";
			close2;
			delitem 745,1; //Wedding Bouquet
			delitem 748,1; //Witherless Rose
			set oversea_event9,2;
			getitem 11705,50; //Children's Potion
			end;
		}
		mes "[Pandit chacha]";
		mes "Pequenos aventureiro, você deve trazer a mim um Buquê de Casamento e uma Rosa Eterna.";
		mes "Se você tra-zelos para mim, eu te darei 50 Poções de Criança. ";
		close;
	}
	mes "[Pandit chacha]";
	mes "Hahaha~";
	mes "Você está gostando da Semana da Criança? ~";
	close;
}