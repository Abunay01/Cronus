//===== Cronus Script ========================================
//= Refinador Avançado
//===== Criado por: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= Hercules e Cronus
//===== Comentarios Adicionais: ==============================
//= 1.0 Traduzido PT-BR.[beckdeveloper]
//============================================================

payon,174,138,0	script	Suhnbi#cash	85,{
	disable_items;
	mes "[Suhnbi]";
	mes "Eu sou o Refinador";
	mes "Eu posso refinar todos os tipos de armas,";
	mes "armaduras e equipamentos, então deixe-me";
	mes "sabe o que quer para refinar.";
	next;
	setarray .@position$[1], "Cabeça","Corpo","Mão Esquerda","Mão Direita","Manto","Sapatos","Acessorio 1","Acessorio 2","Cabeça 2","Cabeça 3";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);

	if( !getequipisequiped(.@part) )
		close;

	if( !getequipisenableref(.@part) )
	{
		mes "[Suhnbi]";
		mes "Eu não acho que eu possa aperfeiçoar todos estes itens.";
		close;
	}
	if( !getequipisidentify(.@part) )
	{
		mes "[Suhnbi]";
		mes "Isto ainda não foi identificado. Assim, ela não pode ser refinado...";
		close;
	}
	if( getequiprefinerycnt(.@part) >= 10 )
	{
		mes "[Suhnbi]";
		mes "Este item não pode ser refinado, pois já atingiu o seu nível máximo...";
		close;
	}

		// Verifique se você tem os itens necessários e zeny para refinar seus itens
		// Determina a chance de falha e verifique se você deseja continuar.
	switch( getequipweaponlv(.@part) )
	{
		case 1: callsub S_RefineValidate,1,7620,50,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,200,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,5000,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,20000,.@part; break;
		default: callsub S_RefineValidate,0,7619,2000,.@part; break;
	}

	if( getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100) )
	{
		mes "[Suhnbi]";
		mes "Clink! Clank! Clunk!";
		SuccessRefItem .@part;
		next;
		Emotion e_no1;
		mes "[Suhnbi]";
		mes "Aqui está! Está feito.";
		mes "Faz muito tempo desde que eu fiz uma arma tão forte bem você deve estar feliz, porque se tornou mais forte!";
		close;
	}
	mes "[Suhnbi]";
	mes "Clink! Clank! Clunk!";
	FailedRefItem .@part;
	next;
	if (rand(5) == 1){
		Emotion e_cash;
	} else {
		Emotion e_omg;
	}
	mes "[Suhnbi]";
	mes "Cough!!!!";
	next;
	mes "[Suhnbi]";
	mes "Cough...Cough..";
	mes "Que vergonha...";
	mes "Seu equipamento quebrou durante o processo de refino que eu havia dito mais cedo que isso poderia acontecer!";
	close;

S_RefineValidate:
	mes "[Suhnbi]";
	if (getarg(0))
		mes "Um nível " + getarg(0) + " arma...";
	mes "Para refinar isso eu preciso de um ^ff9999" + getitemname(getarg(1)) + "^000000 e uma taxa de serviço " + getarg(2) + " Zeny.";
	mes "Você deseja continuar?";
	next;
	if( select("Sim:Não") == 1 )
	{
		if( getequippercentrefinery(getarg(3)) < 100 )
		{
			mes "[Suhnbi]";
			mes "Wow!!";
			mes "Esta arma, provavelmente";
			mes "parece que foi refinada...";
			mes "muitas vezes	...";
			mes "ele pode quebra se";
			mes "for refinado novamente.";
			next;
			mes "E se ele quebra,";
			mes "você não poderar usá-lo mais!";
			mes "Todas a cartas e propriedades do equipamento";
			mes "^ff0000será perdida^000000!!";
			mes "^ff0000 além disso, o equipamento vai quebrar!^000000";
			mes " ";
			mes "Tem certeza que ainda quer continuar?";
			next;
			if( select("Sim:Não") == 2 )
			{
				mes "[Suhnbi]";
				mes "Eu concordo ...";
				mes "Eu posso ser um grande refinador, mas às vezes até eu cometo erros.";
				close;
			}
		}
		if( countitem(getarg(1)) > 0 && Zeny > getarg(2) )
		{
			delitem getarg(1), 1;
			set Zeny, Zeny - getarg(2);
			return;
		}
		mes "[Suhnbi]";
		mes "Você parece não ter Zeny suficiente " + getitemname(getarg(1)) + "...";
		mes "Vá pegar um pouco mais eu vou estar aqui o dia todo, se você precisar de mim.";
		close;
	}
	mes "[Suhnbi]";
	mes "É... Não há necessidade de pressa.";
	close;
}