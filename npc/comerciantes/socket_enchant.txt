//===== Hercules Script ======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= DoJo
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= Hercules Git
//===== Description: =========================================
//= [Conversão do Aegis]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 1.0 Primeira Versão [Dojo]
//
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- No momento, você está carregando -";
		mes "- muitos itens. -";
		mes "- Volte depois após - ";
		mes "- guardar alguns itens no Armazém Kafra. -";
		close;
	}
	mes "[Seiyablem]";
	mes "Sou um engenheiro especializado em";
	mes "abrir slots em armas e armaduras normais.";
	mes "Algumas pessoas me perguntam por que é tão difícil fazer buracos nas armas,";
	mes "mas eu digo que elas não entendem como este trabalho é complicado.";
	next;
	mes "[Seiyablem]";
	mes "Na verdade, tenho certeza que você nunca viu";
	mes "alguém fazer isso antes de mim. O que prova";
	mes "que a fabricação de slots é um trabalho dificílimo.";
	mes "De qualquer forma, você tem algum equipamento no qual queira colocar um slot?";
	mes "A taxa de serviço, os materiais necessários e a chance de sucesso";
	mes "variam de acordo com o equipamento.";
	mes "Primeiro, escolha para qual equipamento você quer o slot.";
	next;
	switch(select("Arma:Armadura:Mais Informaçõe:Sair"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "Arma, você disse? Claro, será um prazer.";
            mes "As armas são divididas em 4 classes diferentes,";
			mes "de acordo com a dificuldade do trabalho.";
			mes "A classe C é a mais fácil, e a classe S é a mais difícil.";
			mes "Qual classe você quer tentar?";
			next;
			switch(select("Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Classe C, entendi. Então, que tipo de arma você tem?";
					next;
					switch(select("Tridente:Cordel:Violino"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Você escolheu a classe B.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Mangual Pesado:Gladius:Gakkung:Pique:Haedonggum:Alaúde:Açoite:Waghnak:Arbaleste"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Classe A? Você parece querer demais. Mas não tem problema.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Arco de Caçador:Bastão do Sobrevivente(INT):Zweihander:Espada Flamberge:Infiltrador:Balista:Atordoador:Fúria Insana:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "Quê... mas... Classe S?! *Treme*";
					mes "Então... para qual arma de classe S você quer fazer um slot?!";
					next;
					switch(select("Gungnir:Adaga Venenosa:Rondel:Sucsamad:Ginnungagap:Cutelo:Foice:Bastão do Sobrevivente(DES)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "Armadura, você disse? Claro, será um prazer.";
			mes "As armaduras são divididas em 4 classes diferentes,";
			mes "de acordo com a dificuldade do trabalho.";
			mes "A classe C é a mais fácil, e a classe S é a mais difícil.";
			mes "Qual classe você quer tentar?";
			next;
			switch(select( "Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Classe C, entendi. Então, que tipo de armadura você tem?";
					next;
					switch(select("Capote:Casaco:Tiara:Solidéu"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Você escolheu a classe B.";
                    mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Escudo Espelhado:Cota de Malha:Manto Sagrado:Manto de Seda:Botas:Sapatos:Sobrepeliz:Vembrassa:Broquel:Escudo:Chapéu de Bongun"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Classe A? Você parece querer demais. Mas não tem problema.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Capacete de Schallern:Chapéu de Balde:Grimório:Malha:Armadura Legionária:Armadura Metálica:Traje de Gatuno:Grevas:Chapéu de Freira:Manteau:Capacete:Indumentária Ninja:Capacete de Orc:Capa Velha:Chapéu de Monge:Peça Dourada:Broche:Chapéu de Munak"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "Quê... mas... Classe S?! *Treme*";
					mes "Então... para qual armadura de classe S você quer fazer um slot?!";
					next;
					switch(select("Chifres Majestosos:Chifres Pontudos:Elmo de Osso:Chapéu de Corsário:Coroa:Diadema:Coroa de Faraó :Manto da Conjuração:Brinco:Anel:Luva de Puxada"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "Vou dar uma dica para você.";
			mes "Já viajei a muitos lugares,";
			mes "e abri slots em armas e armaduras.";
			mes "Trabalhando nesta cidade de Payon há algum tempo,";
			mes "percebi que equipamentos com aprimoramento maior";
			mes "podem ter uma chance de sucesso maior para abrir slots.";
			next;
			mes "[Seiyablem]";
			mes "A diferença é bem sutil, mas sinto que";
			mes "tenho mais sorte ao trabalhar com equipamentos mais aprimorados, sabe?";
			mes "Claro, há o risco do equipamento se perder";
			mes "se falharmos ao criar um slot.";
			mes "Só estou falando da chance de sucesso.";
			next;
			mes "[Seiyablem]";
			mes "Em algumas cidades, há uma chance de sucesso maior";
			mes "para abrir slots em equipamentos menos aprimorados.";
			mes "Mas Payon não é uma dessas cidades.";
			next;
			mes "[Seiyablem]";
			mes "E há mais uma coisa que você precisa saber.";
			mes "Vou dar um exemplo.";
			mes "Digamos que você tenha um Manteau +7 e um Manteau normal.";
			mes "E você me peça para abrir um slot em um dos Manteaus.";
			mes "Então, em qual Manteau você acha que eu trabalharia?";
			next;
			mes "[Seiyablem]";
			mes "A resposta: isso é aleatório.";
			mes "Ou seja, se você tivesse mais de";
			mes "um equipamento do mesmo nome em seu inventário,";
			mes "eu usaria um deles, aleatoriamente, para abrir um slot,";
			mes "você goste ou não.";
			next;
			mes "[Seiyablem]";
			mes "^FF0000Portanto, se você quiser abrir um slot em um equipamento específico,";
			mes "^FF0000trate de trazer somente esse item para mim,";
			mes "^FF0000e deixe os outros equipamentos do mesmo nome em seu armazém.";
			next;
			mes "[Seiyablem]";
			mes "Se você não me der ouvidos,";
			mes "e trouxer mais de um item do mesmo nome,";
			mes "não vou assumir a responsabilidade pelo resultado indesejado.";
			next;
			mes "[Seiyablem]";
			mes "Você não precisa de mais explicações, precisa?";
			mes " É preciso ver para crer! Então, por que não tenta criar um slot,";
			mes "e me ajuda a ganhar a vida, hein?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "Bah!";
			close;
	}
}

function	script	Func_Socket	{

	disable_items;
	mes "[Seiyablem]";
	mes " " + getitemname(getarg(0)) + "? Certo, então você deve me trazer";
	mes "^FF0000" + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + "^000000 ") : "") + "e ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zenys^000000 como taxa de serviço.";
	mes " É claro, não esqueça de me trazer um " + getitemname(getarg(0)) + "!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, é uma arma
	{
		mes "Você já trouxe tudo?";
		mes "Preciso avisar: se falharmos em criar um slot,";
		mes "você perderá a ^FF0000Warma e todo o material usado.^000000";
		mes "Lembre-se também que, se a arma tiver sido aprimorada, e tiver sido equipada com uma carta,";
		mes "você a perderá mesmo se conseguir criar um slot.";
	}
	else // armadura
	{
		mes "Você já trouxe tudo?";
		mes "Preciso avisar: se falharmos em criar um slot,";
		mes "você perderá a ^FF0000armadura e todo o material usado.^000000";
		mes "Lembre-se também que, se a armadura tiver sido aprimorada, e tiver sido equipada com uma carta,";
		mes "você a perderá mesmo se conseguir criar um slot.";
	}
	next;
	switch(select("Pedir criação de slot.:Tentar da próxima vez."))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "Muito bem, então que o trabalho comece!";
				mes "Reze para conseguir um bom resultado.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "Ótimo, parece que deu certo.";
					mes "O trabalho saiu muito bem feito. Parabéns!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "Te vejo depois, camarada!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "Ugh! ... Sinto muito, falhou.";
					mes "Mas eu não tive culpa alguma.";
					mes "Isso tem a ver com sua sorte e com seu destino, certo?";
					mes "Não fique tão desapontado, tente de novo da próxima vez.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[Seiyablem]";
					mes "Te desejo boa sorte da próxima vez!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "Você é tapado ou o quê? Você não trouxe todo o material necessário!";
				mes "Traga tudo, e rápido!";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "Até mais.";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Jovem#dummy::SocketMan	-1,{
	mes "[Jovem]";
	mes "Criar um slot em um equipamento";
	mes "pode alterar sua opção.";
	next;
	mes "[Jovem]";
	mes "Ah, este caso é extremamente raro,";
	mes "mas às vezes você pode criar";
	mes "2 slots em um equipamento de uma só vez.";
	mes " É claro, nem todo equipamento oferece esta chance.";
	next;
	mes "[Jovem]";
	mes "Estou pensando em estudar";
	mes "para me tornar um engenheiro criador de slots.";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Jovem#prt0	97
payon,143,143,7	duplicate(SocketMan)	Jovem#pay0	97
morocc,60,42,3	duplicate(SocketMan)	Jovem#moc0	97
lhz_in02,269,33,5	duplicate(SocketMan)	Jovem#lhz0	97