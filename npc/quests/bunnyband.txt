//===== Cronus Script ======================================
//= Orelhas de Coelho
//===== Por: ================================================== 
//= Cronus
//===== Versão Atual: ===================================== 
//= 1.6
//===== Descrição: ========================================= 
//= [Aegis Conversion]
//= Quest para obter as Orelhas de Coelho.
//===== Comentários Adicionais: ================================= 
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//	Changed @BUNYBAN back to BUNYBND, it should be this way.
//	Removed duplicates. NPC is ALberta-only.
//= 1.6 Cleaning. [Euphy]
//= 1.7 Tradução PT-BR  [Ericles Silva]
//============================================================ 

alberta,26,229,0	script	Funcionário Kafra#bunny	83,{
	if (BUNYBND == 1) {
		mes "[Funcionário Kafra]";
		mes "Olá você aí!~!";
		mes "Você gostaria de";
		mes "participar do Evento especial da";
		mes "^529DFFOrelha de Coelho^000000 da Corporação Kafra?";
		next;
		switch(select("Claro, eu trouxe os itens.:Informações do Evento.:Cancelar")) {
		case 1:
			mes "[Funcionário Kafra]";
			mes "Ok.~";
			mes "Deixe-me verificar";
			mes "vendo se você trouxe";
			mes "todos os itens...";
			next;
			setarray .@Items[0],949,100,706,1,722,1,2213,1;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
				mes "[Funcionário Kafra]";
				mes "Ooh, eu sinto muito";
				mes "mas você precisa";
				mes "trazer pelo menos";
				mes .@Items[.@i+1]+" "+getitemname(.@Items[.@i])+((!.@i)?"s.":".");
					close;
				}
			mes "[Funcionário Kafra]";
			mes "Ótimo, eu vejo que";
			mes "você pegou";
			mes "tudo que eu preciso";
			mes "para fazer a Orelha de Coelho.";
			mes "Por favor espere um momento";
			mes "enquanto eu coloco eles juntos...";
			next;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
					mes "[Kafra Employee]";
					mes "Hm?  Sinto Muito,";
					mes "mas eu realmente não posso";
					mes "fazer isso agora. Você";
					mes "precisa "+.@Items[.@i+1]+" "+getitemname(.@Items[.@i])+((!.@i)?"s":"");
					mes "em ordem para eu colocar isso";
					mes "junto na Orelha de Coelho..";
					close;
				}
			delitem 949,100; //Pluma
			delitem 706,1; //Trevo de Quatro Folhas
			delitem 722,1; //Pérola
			delitem 2213,1; //Orelha de Gato
			mes "[Kafra Employee]";
			mes "Ah, aqui vai!~";
			mes "A perfeita Orelha de Coelho!";
			mes "Bem, espero que gostem.";
			getitem 2214,1;  // Orelha de Coelho
			set BUNYBND,0;
			next;
			mes "[Funcionário Kafra]";
			mes "Obrigado por";
			mes "participar deste";
			mes "evento especial e que";
			mes "seu patrocínio continue";
			mes "nos Serviços Kafra~";
			close;
		case 2:
			goto L_Info;
		case 3:
			close;
		}
	}
	mes "[Funcionário Kafra]";
	mes "Oi você aí~! Você gostaria";
	mes "de juntar-se ao nosso especial ^529DFFEvento da Orelha de Coelho^000000 feito pela Corporação";
	mes "Kafra e patrocinado pela";
	mes "a Corporação de Mercadores de Alberta?";
	next;
	switch(select("Entrar no Evento.:Informação do Evento.:Cancelar.")) {
	case 1:
		set BUNYBND,1;
		mes "[Funcionário Kafra]";
		mes "Ótimo. Obrigado por";
		mes "participar! Se você";
		mes "ainda não ouviu, você";
		mes "precisa coletar esses itens,";
		mes "caso você deseje que eu coloque";
		mes "uma Orelha de Coelho em você!...";
		next;
		mes "[Funcionário Kafra]";
		mes "100 Pulmas,";
		mes "1 Trevo de Quatro Folhas,";
		mes "1 Orelha de Gato e";
		mes "1 Pérola. É isso aí!";
		mes "Boa sorte e eu vou estar";
		mes "esperando por você aqui.~";
		close;
	case 2:
		break;
	case 3:
		close;
	}
L_Info:
	mes "[Funcionário Kafra]";
	mes "Para agradecer os nossos valiosos";
	mes "clientes, Corporação Kafra";
	mes "preparou um evento especial";
	mes "onde a Funcionário Kafra irá montar";
	mes "Orelhas de Coelho para os aventureiros";
	mes "que trouxerem os itens necessários.";
	next;
	mes "[Funcionário Kafra]";
	mes "Para este tipo de item ";
	mes "muito especial, você deve";
	mes "trazer 100 Plumas,";
	mes "1 Trevo de Quatro Folhas,";
	mes "1 Orelha de Gato e";
	mes "1 Pérola.";
	next;
	mes "[Funcionário Kafra]";
	mes "Quando estiver pronto,";
	mes "volte e traga";
	mes "os itens para mim.";
	mes "(Patrocinado pela";
	mes "Corporação de Mercadores de Alberta).";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 using duplicate command
//= 1.2 Fixed NPC location, removed NPC dupes [Lupus]
//= 1.3 RE-Fixed exploit V_V, also cleared used VAR [Lupus]
//= 1.4 replaced BUNYBND with @BUNYBND [Lupus]
//============================================================