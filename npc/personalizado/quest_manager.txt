//===== Cronus Script =========================================
//= Quest Manager
//===== Por: ==================================================
//= Zawe
//===== Versão Atual: =========================================
//= 0.1
//===== Descrição: ==========================================
//= Shop Quest Manager. Com suporte de apenas 1 Item de Reward 
//= e mais ou menos 70 de Requerimento,  possivel escolher o 
//= item atravez de um Shop. No caso de falta de items 
//= requeridos, o script avisara ao jogador que items faltam.
//===== Comentários Adicionais: ===============================
//= 0.1 Criação do Script [Zawe]
//===== Observações: ==========================================
//= Para configurar siga o exemplo:
//= Ex: Add(535,1,536,1,537,1); - Recebe 1 Abóbora(535) e perde
//= 1 Sorvete(536) e 1 Ração para Bixinho(537)
//= Mais informações nas linhas 29 e 30.
//=============================================================
prontera,155,170,5	script	Quest Manager	965,{
function Add;
callshop "qshop",1;
npcshopattach "qshop";
end;
 
OnInit:
    npcshopdelitem "qshop",537;
    // Não mexa em nada acima disso
    // Add(Reward ID,Reward Amount,Required ID,Required Amount,{,...}); {,...} = Infinitamente como Required ID, Required Amount
    // Ex: Add(535,1,536,1,537,1); - Recebe 1 Abóbora(535) e perde 1 Sorvete(536) e 1 Ração para Bixinho(537)
    end;

OnBuyItem:
    if(getarraysize(@bought_nameid) > 1) {
        mes "Por favor escolha só um item.";
        close;
    }
    mes "["+strnpcinfo(1)+"]";
    mes "Quest: "+getd(".q_"+@bought_nameid+"[1]")+"x "+getitemname(getd(".q_"+@bought_nameid+"[0]"));
    mes "Requerimentos:";
    for(set .@i,2; .@i < getarraysize(getd(".q_"+@bought_nameid+"")); set .@i,.@i + 2) {
        mes ""+getd(".q_"+@bought_nameid+"["+(.@i+1)+"]")+"x "+getitemname(getd(".q_"+@bought_nameid+"["+(.@i)+"]"))+"";
    }
    switch(select("~ Comprar:~ Sair")) {
		case 1:
			for(set .@i,0; .@i < ( getarraysize(getd(".q_"+@bought_nameid+"")) - 2 ); set .@i,.@i + 2) {
				if(countitem(getd(".q_"+@bought_nameid+"["+(.@i+2)+"]")) < getd(".q_"+@bought_nameid+"["+(.@i+3)+"]")) {
					setarray .@j[.@i],getd(".q_"+@bought_nameid+"["+(.@i+2)+"]"),( getd(".q_"+@bought_nameid+"["+(.@i+3)+"]") - countitem(getd(".q_"+@bought_nameid+"["+(.@i+2)+"]")) );
				}
			}
			if(.@j) {
				mes "Falta:";
				for(set .@i,0; .@i < getarraysize(.@j); set .@i,.@i + 2) {
					mes .@j[.@i+1]+"x "+.@j[.@i];
				}
				close;
			}
			for(set .@i,2; .@i < ( getarraysize(getd(".q_"+@bought_nameid+"")) - 1 ); set .@i, .@i + 2) {
				delitem getd(".q_"+@bought_nameid+"["+(.@i)+"]"),getd(".q_"+@bought_nameid+"["+(.@i+1)+"]");
			}
			getitem getd(".q_"+@bought_nameid+"[0]"),getd(".q_"+@bought_nameid+"[1]");
			close;
			
		case 2:
			close;
	}
 
function	Add	{
    if(getargcount()%2) { debugmes "Something went wrong. Theres is something wrong with the Code."; return; }
    for(set .@i,0; .@i < getargcount(); set .@i,.@i + 1) {
        if(.@i%2 == 0) {
			if(getitemname(getarg(.@i)) == "null") { debugmes "Something went wrong. One of the Items ID do not exist."; return; }
        }
    }
    for(set .@i,0; .@i < getargcount(); set .@i,.@i + 1) {
        setarray .@j[.@i],getarg(.@i);
    }
    copyarray getd(".q_"+getarg(0)+"[0]"),.@j[0],getarraysize(.@j);
    npcshopadditem "qshop",getarg(0),1;
    return;
}
}

-	shop	qshop	1,537:-1